## <summary>Super user administration role (DEBUG ONLY - DONT USE IN PRODUCTION)</summary>

########################################
## <summary>
##	Change to the system administrator role.
## </summary>
## <param name="role">
##	<summary>
##	Role allowed access.
##	</summary>
## </param>
## <rolecap/>
#
interface(`toor_role_change',`
	gen_require(`
		role toor_r;
	')

	allow $1 toor_r;
')

########################################
## <summary>
##	Change from the system administrator role.
## </summary>
## <desc>
##	<p>
##	Change from the system administrator role to
##	the specified role.
##	</p>
##	<p>
##	This is an interface to support third party modules
##	and its use is not allowed in upstream reference
##	policy.
##	</p>
## </desc>
## <param name="role">
##	<summary>
##	Role allowed access.
##	</summary>
## </param>
## <rolecap/>
#
interface(`toor_role_change_to',`
	gen_require(`
		role toor_r;
	')

	allow toor_r $1;
')

########################################
## <summary>
##	Execute a shell in the toor domain.
## </summary>
## <param name="domain">
##	<summary>
##	Domain allowed access.
##	</summary>
## </param>
#
interface(`toor_shell_domtrans',`
	gen_require(`
		type toor_t;
	')

	corecmd_shell_domtrans($1, toor_t)
	allow toor_t $1:fd use;
	allow toor_t $1:fifo_file rw_file_perms;
	allow toor_t $1:process sigchld;
')

########################################
## <summary>
##	Execute a generic bin program in the toor domain.
## </summary>
## <param name="domain">
##	<summary>
##	Domain allowed access.
##	</summary>
## </param>
#
interface(`toor_bin_spec_domtrans',`
	gen_require(`
		type toor_t;
	')

	corecmd_bin_spec_domtrans($1, toor_t)
	allow toor_t $1:fd use;
	allow toor_t $1:fifo_file rw_file_perms;
	allow toor_t $1:process sigchld;
')

########################################
## <summary>
##	Execute all entrypoint files in the toor domain. This
##	is an explicit transition, requiring the
##	caller to use setexeccon().
## </summary>
## <param name="domain">
##	<summary>
##	Domain allowed access.
##	</summary>
## </param>
#
interface(`toor_entry_spec_domtrans',`
	gen_require(`
		type toor_t;
	')

	domain_entry_file_spec_domtrans($1, toor_t)
	allow toor_t $1:fd use;
	allow toor_t $1:fifo_file rw_file_perms;
	allow toor_t $1:process sigchld;
')

########################################
## <summary>
##	Allow toor to execute all entrypoint files in
##	a specified domain.  This is an explicit transition,
##	requiring the caller to use setexeccon().
## </summary>
## <desc>
##	<p>
##	Allow toor to execute all entrypoint files in
##	a specified domain.  This is an explicit transition,
##	requiring the caller to use setexeccon().
##	</p>
##	<p>
##	This is a interface to support third party modules
##	and its use is not allowed in upstream reference
##	policy.
##	</p>
## </desc>
## <param name="domain">
##	<summary>
##	Domain allowed access.
##	</summary>
## </param>
#
interface(`toor_entry_spec_domtrans_to',`
	gen_require(`
		type toor_t;
	')

	domain_entry_file_spec_domtrans(toor_t, $1)
	allow $1 toor_t:fd use;
	allow $1 toor_t:fifo_file rw_file_perms;
	allow $1 toor_t:process sigchld;
')

########################################
## <summary>
##	Allow toor to execute a generic bin program in
##	a specified domain.  This is an explicit transition,
##	requiring the caller to use setexeccon().
## </summary>
## <desc>
##	<p>
##	Allow toor to execute a generic bin program in
##	a specified domain.
##	</p>
##	<p>
##	This is a interface to support third party modules
##	and its use is not allowed in upstream reference
##	policy.
##	</p>
## </desc>
## <param name="domain">
##	<summary>
##	Domain to execute in.
##	</summary>
## </param>
#
interface(`toor_bin_spec_domtrans_to',`
	gen_require(`
		type toor_t;
	')

	corecmd_bin_spec_domtrans(toor_t, $1)
	allow $1 toor_t:fd use;
	allow $1 toor_t:fifo_file rw_file_perms;
	allow $1 toor_t:process sigchld;
')

########################################
## <summary>
##	Send a SIGCHLD signal to toor users.
## </summary>
## <param name="domain">
##	<summary>
##	Domain allowed access.
##	</summary>
## </param>
#
interface(`toor_sigchld',`
	gen_require(`
		type toor_t;
	')

	allow $1 toor_t:process sigchld;
')

########################################
## <summary>
##	Inherit and use toor file descriptors
## </summary>
## <param name="domain">
##	<summary>
##	Domain allowed access.
##	</summary>
## </param>
#
interface(`toor_use_fds',`
	gen_require(`
		type toor_t;
	')

	allow $1 toor_t:fd use;
')

########################################
## <summary>
##	Read and write toor user unnamed pipes.
## </summary>
## <param name="domain">
##	<summary>
##	Domain allowed access.
##	</summary>
## </param>
#
interface(`toor_rw_pipes',`
	gen_require(`
		type toor_t;
	')

	allow $1 toor_t:fifo_file rw_fifo_file_perms;
')
