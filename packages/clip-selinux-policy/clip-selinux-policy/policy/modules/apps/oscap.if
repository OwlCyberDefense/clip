
## <summary>policy for oscap</summary>

########################################
## <summary>
##	Transition to oscap.
## </summary>
## <param name="domain">
## <summary>
##	Domain allowed to transition.
## </summary>
## </param>
#
interface(`oscap_domtrans',`
	gen_require(`
		type oscap_t, oscap_exec_t;
	')

	corecmd_search_bin($1)
	domtrans_pattern($1, oscap_exec_t, oscap_t)
')

########################################
## <summary>
##	Transition to oscap_probe_t.
## </summary>
## <param name="domain">
## <summary>
##	Domain allowed to transition.
## </summary>
## </param>
#
interface(`oscap_probe_domtrans',`
	gen_require(`
		type oscap_probe_t, oscap_probe_exec_t;
	')

	corecmd_search_bin($1)
	domtrans_pattern($1, oscap_probe_exec_t, oscap_probe_t)
	allow $1 oscap_probe_t:process signal;
	allow oscap_probe_t $1:unix_stream_socket create_stream_socket_perms;
')

########################################
## <summary>
##	Execute oscap in the oscap domain, and
##	allow the specified role the oscap domain.
## </summary>
## <param name="domain">
##	<summary>
##	Domain allowed to transition
##	</summary>
## </param>
## <param name="role">
##	<summary>
##	The role to be allowed the oscap domain.
##	</summary>
## </param>
#
interface(`oscap_run',`
	gen_require(`
		type oscap_t;
	')

	oscap_domtrans($1)
	role $2 types oscap_t;
')

########################################
## <summary>
##	This interface simply allows the passed-in role to 
##      associated with the oscap_probe_t domain.
##      This is mainly used for statisfying the conditional
##      policy constraint where you can't conditionally a
##      associate a role with a domain.
##      
##      Note: typically you see this called outside a 
##      conditional, and oscap_prob_domtrans() called
## </summary>
## <param name="role">
##	<summary>
##	The role to be allowed the oscap_probe domain.
##	</summary>
## </param>
#
interface(`oscap_probe_role_assoc',`
	gen_require(`
		type oscap_probe_t;
	')

	role $1 types oscap_probe_t;
')

########################################
## <summary>
##	Execute oscap probes in the oscap_probe domain, and
##	allow the specified role the oscap_probe domain.
## </summary>
## <param name="domain">
##	<summary>
##	Domain allowed to transition
##	</summary>
## </param>
## <param name="role">
##	<summary>
##	The role to be allowed the oscap_probe domain.
##	</summary>
## </param>
#
interface(`oscap_probe_run',`
	gen_require(`
		type oscap_probe_t;
	')

	oscap_probe_domtrans($1)
	oscap_probe_role_assoc($2)
')

########################################
## <summary>
##	Role access for oscap
## </summary>
## <param name="role">
##	<summary>
##	Role allowed access
##	</summary>
## </param>
## <param name="domain">
##	<summary>
##	User domain for the role
##	</summary>
## </param>
#
# TODO rename or remove
interface(`oscap_role',`
	gen_require(`
		type oscap_t;
	')

	role $1 types oscap_t;

	oscap_domtrans($2)

	ps_process_pattern($2, oscap_t)
	allow $2 oscap_t:process signal;
')


########################################
## <summary>
##	Allow oscap domain gettatr perms on var_log and audit_log.
## </summary>
## <param name="domain">
##	<summary>
##	Domain allowed permissions
##	</summary>
## </param>
#
interface(`oscap_read_all_files_except',`
        gen_require(`
                type oscap_probe_t;
                type shadow_t;
		type var_log_t;
                type auditd_log_t;
        ')
	
	files_read_all_files_except($1, { -shadow_t -auditd_log_t -var_log_t })
	allow $1 { shadow_t var_log_t auditd_log_t }:file getattr;
')

